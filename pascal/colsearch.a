
;--- colsearch.a --------------------------------------------------------+
; MediaPoint Color Find Routines. Code written by B.O.M.B. AudioVisual   |
; Entertainment and 1001 Software Development.                           |
;------------------------------------------------------------------------+
; DISCLAIMER: At the moment there is no guarantee that this code is 100% |
; correct and doesn't corrupt the system. Though much effort has been    |
; made to get it right, it's almost impossible to test for all possible  |
; situations. In fact, I may be totally out of my little mind !!         |
;------------------------------------------------------------------------+
; All programming by:                                                    |
;    Pascal Eeftinck, Bits On My Byte AudioVisual Entertainment          |
;    Cees Lieshout, MediaPoint Inc.                                      |
;------------------------------------------------------------------------+

; $VER: mediapoint/pascal/colsearch.a 0.038 (02.18.94)


	TTL	colsearch.a
	IDNT	colsearch.a


	INCDIR "include:"

	INCLUDE	"exec/types.i"
	INCLUDE	"exec/funcdef.i"
	INCLUDE	"exec/memory.i"
	INCLUDE	"exec/exec_lib.i"
	INCLUDE	"exec/libraries.i"
	INCLUDE	"graphics/gfx.i"
	INCLUDE	"graphics/view.i"
	INCLUDE	"graphics/graphics_lib.i"

	INCDIR	"pascal:include/"

	INCLUDE	"macros.i"
	INCLUDE	"stackvars.i"
	INCLUDE	"colsearch.i"
	INCLUDE	"scarem.i"


	XREF	@GetRGB
	XREF	@AllocRememberMP
	XREF	@FreeRememberMP


	XDEF	_InitBaseColor
	XDEF	_FindColorBest
	XDEF	_FindColorBestOpaque
	XDEF	_AllocateTable
	XDEF	_InvalidateTable
	XDEF	_ReleaseTable
	XDEF	_CMapToUWORDS
	XDEF	_CreateFastTable


	SECTION	TEXT,CODE


  STACKSTRUCT
	_ULONG	initb_Blu32
	_ULONG	initb_Grn32
	_ULONG	initb_Red32
	_LABEL	initb_RGBtriplet
	_LABEL	initb_SIZEOF

_InitBaseColor
	link	a5,#initb_SIZEOF
	movem.l	a2,-(sp)
	movea.l	_1stParam(a5),a2
	clr.w	cth_BaseRED(a2)
	clr.w	cth_BaseGRN(a2)
	clr.w	cth_BaseBLU(a2)
	tst.l	cth_ColorMap(a2)
	beq	.exit

	movea.l	cth_ColorMap(a2),a0
	moveq.l	#0,d0
	moveq.l	#1,d1
	lea.l	initb_RGBtriplet(a5),a1
	jsr	@GetRGB
	move.b	initb_Red32(a5),cth_BaseRED+1(a2)
	move.b	initb_Grn32(a5),cth_BaseGRN+1(a2)
	move.b	initb_Blu32(a5),cth_BaseBLU+1(a2)

.exit	movem.l	(sp)+,a2
	unlk	a5
	rts



;---------------------------------------------------------------------------
; VOID CMapToUWORDS( struct ColorTableHeader *(a0), vm (d1) )
;---------------------------------------------------------------------------
; Fills array with colors from colormap.
;---------------------------------------------------------------------------

  STACKSTRUCT
	_ULONG	cmw_BLU
	_ULONG	cmw_GRN
	_ULONG	cmw_RED
	_LABEL	cmw_RGBtriplet
	_LABEL	cmw_SIZEOF

_CMapToUWORDS
	link	a5,#cmw_SIZEOF
	movem.l	d0-d4/a0-a1/a3,-(sp)
	movea.l	a0,a3
	tst.l	cth_ColorMap(a3)
	beq	.exit
	tst.l	cth_ColorTable(a3)
	beq	.exit
	movea.l	cth_ColorMap(a3),a0
	movea.l	cth_ColorTable(a3),a1
	move.l	cth_Size(a3),d0
	cmpi.l	#2^24,d0
	beq	.exit

	andi.w	#V_EXTRA_HALFBRITE,d1
	sne.b	d3
	andi.w	#1,d3

	moveq.l	#0,d2

.loop01	movem.w	d0/d3,-(sp)
	move.l	d2,d0
	tst.w	d3
	beq.b	.noEHB2
	cmpi.w	#31,d0
	bls.b	.noEHB2
	subi.w	#32,d0
.noEHB2	movem.l	a0-a1,-(sp)
	lea.l	cmw_RGBtriplet(a5),a1
	moveq.l	#1,d1
	jsr	@GetRGB
	movem.l	(sp)+,a0-a1
	cmpi.w	#31,d2
	shi.b	d4
	and.b	d4,d3
	moveq.l	#0,d1
	move.b	cmw_RED(a5),d1
	lsr.b	d3,d1
	move.w	d1,(a1)+
	move.b	cmw_GRN(a5),d1
	lsr.b	d3,d1
	move.w	d1,(a1)+
	move.b	cmw_BLU(a5),d1
	lsr.b	d3,d1
	move.w	d1,(a1)+
	clr.w	(a1)+
	movem.w	(sp)+,d0/d3

	addq.w	#1,d2
	cmp.w	d2,d0
	bne.b	.loop01
	move.w	#-1,(a1)+

.exit	movem.l	(sp)+,d0-d4/a0-a1/a3
	unlk	a5
	rts


_AllocateTable
	lea.l	GlobalColorTable(pc),a0
	tst.l	(a0)
	bne.b	.exit
	move.l	#50000,d0
	move.l	#MEMF_ANY!MEMF_PUBLIC,d1
	jsr	@AllocRememberMP
	lea.l	GlobalColorTable(pc),a0
	move.l	d0,(a0)
	lea.l	GlobalTableValid(pc),a0
	move.l	#FALSE,(a0)
	lea.l	GlobalTableOpaque(pc),a0
	clr.b	(a0)
.exit	rts

_InvalidateTable
	lea.l	GlobalTableValid(pc),a0
	move.l	#FALSE,(a0)
	rts

_ReleaseTable
	lea.l	GlobalColorTable(pc),a0
	tst.l	(a0)
	beq.b	.exit
	movea.l	GlobalColorTable(pc),a0
	jsr	@FreeRememberMP
	lea.l	GlobalColorTable(pc),a0
	clr.l	(a0)
	lea.l	GlobalTableValid(pc),a0
	move.l	#FALSE,(a0)
.exit	rts




*
* The lookup table stores the matching color-pens for the high 4 bits of
* each red, green and blue values.
* r7-r4g7-g4b7-b4 thus being 4096 entries
* each entry should have space for 7 colors + a count = 8 entries per block
* each entry starts with a number indicating the number of different pens
* For this test a 4096 * 8 = 32768 bytes are needed
* plus the table itself   = 4096 * 4
*			+ 49152 bytes total
*
_CreateFastTable
	movem.l	d0-d7/a0-a6,-(sp)
	movea.l	a0,a5

	lea.l	_FindColorFast(pc),a1
	andi.w	#V_HAM,d1
	beq	.storeFnc
	lea.l	_FindColorFastHAM6(pc),a1
	cmpi.l	#64,cth_Size(a5)
	bne.b	.storeFnc
	lea.l	_FindColorFastHAM8(pc),a1
.storeFnc	move.l	a1,cth_ColorFunc(a5)

	bsr	_AllocateTable
	movea.l	GlobalColorTable(pc),a0
	cmpa.w	#0,a0
	beq	.error

	btst.b	#SCAREMB_OPAQUE,sri_Flags+3(a4)
	sne.b	d0
	lea	GlobalTableOpaque(pc),a3
	move.b	(a3),d1
	move.b	d0,(a3)
	cmp.b	d0,d1
	bne	.invalid
	lea.l	GlobalTableValid(pc),a1
	tst.l	(a1)
	bne	.exit


.invalid	movea.l	a0,a1
	adda.l	#4*4096,a1	; point to first free area
	moveq.l	#8,d0		; required space for color table
	move.l	#4095,d7	; entries in the table

.rep_tt:	move.l	a1,(a0)+
	clr.l	(a1)		; set entries in sub table on 0
	adda.w	d0,a1		; next sub table
	dbf	d7,.rep_tt

; Find for every centre of the small box in the colorspace
; the best fitting color

	moveq.l	#15,d7
	moveq.l	#8,d0

.rep3:	moveq.l	#15,d6
	moveq.l	#8,d1

.rep2:	moveq.l	#15,d5
	moveq.l	#8,d2

.rep1:	movem.w	d0/d1,-(sp)
	movea.l	a5,a0
	btst.b	#SCAREMB_OPAQUE,sri_Flags+3(a4)
	bne.b	.opaque
.normal	bsr	_FindColorBest
	bra.b	.cont
.opaque	bsr	_FindColorBestOpaque
.cont	move.w	d0,d4
	movem.w	(sp)+,d0/d1
	bsr	store_in_table		; place color in table
	addi.w	#16,d2
	dbra	d5,.rep1
	addi.w	#16,d1
	dbra	d6,.rep2
	addi.w	#16,d0
	dbra	d7,.rep3

; each centre point has its own nearest color
; now store the colors in de colormap in the square

	move.l	cth_ColorTable(a5),a2
	addq.l	#8,a2
	moveq	#1,d4			; pen number

.rep_cc:	move.w	(a2)+,d0		; red
	bmi	.no_more
	move.w	(a2)+,d1		; green
	move.w	(a2)+,d2		; blue
	addq.l	#2,a2
	bsr	store_in_table
	addq.w	#1,d4
	bra	.rep_cc

.no_more:	movea.l	GlobalColorTable(pc),a0		; pointer to lookup table
	move.w	#4095,d7		; entries in the table

.rep_tt2:	movea.l	(a0)+,a1
	addi.b	#1,(a1)			; add one for easy search test
	dbra	d7,.rep_tt2

	lea.l	GlobalTableValid(pc),a1
	move.l	#TRUE,(a1)

.exit:	movem.l	(sp)+,d0-d7/a0-a6
	rts

.error	pea.l	_FindColorBest(pc)
	move.l	(sp)+,cth_ColorFunc(a5)
	bra.b	.exit


*
* RGB d0,d1,d2 PenNr. d4
*
store_in_table:
	movem.l	d5/d6,-(sp)
	movea.l	GlobalColorTable(pc),a0
	move.w	d0,d3
	and.w	#$f0,d3
	lsl.w	#6,d3
	move.w	d1,d5
	and.w	#$f0,d5
	lsl.w	#2,d5
	or.w	d5,d3
	move.w	d2,d5
	and.w	#$f0,d5
	lsr.w	#2,d5
	or.w	d5,d3

	movea.l	(a0,d3.w),a0

	moveq.l	#0,d5
	movea.l	a0,a1
	move.b	(a0),d5			; Nr of entries
	beq	.store_it
	addq.w	#1,a1			; skip number of entries
	cmp.b	#7,d5
	beq	.exit			; no more than 7 entries

.do_srch:	cmp.b	(a1)+,d4		; if already stored skip it
	beq	.exit
	subq.b	#1,d5
	bne	.do_srch

.store_it	add.b	#1,(a0)
	move.b	(a0),d5
	adda.w	d5,a0
	move.b	d4,(a0)

.exit:	movem.l	(sp)+,d5/d6
	rts



*
* Find the right RGB square and then find best pen from that square
*
_FindColorFast:
	movem.w	d2-d7,-(sp)
	movem.l	a2/a3/a5,-(sp)

	movea.l	a0,a5
	lea.l	OFlowTable(pc),a2
	lea.l	PythTable(pc),a3

	move.b	(a2,d0.w),d0
	move.b	(a2,d1.w),d1
	move.b	(a2,d2.w),d2
	and.w	#$FF,d0
	and.w	#$FF,d1
	and.w	#$FF,d2
	move.w	d0,cth_DiffsRED(a5)
	move.w	d1,cth_DiffsGRN(a5)
	move.w	d2,cth_DiffsBLU(a5)

	movea.l	GlobalColorTable(pc),a2
	move.w	#$F0,d3
	and.w	d3,d0
	and.w	d3,d1
	and.w	d3,d2
	lsl.w	#6,d0
	lsl.w	#2,d1
	lsr.w	#2,d2
	or.w	d1,d0
	or.w	d2,d0
	movea.l	(a2,d0.w),a2

	cmp.b	#2,(a2)			; when there are two entries you should
	bne	find_best_entry		; select the best from the square

	moveq.l	#0,d0
	move.b	1(a2),d0
	lsl.w	#3,d0
	movea.l	cth_ColorTable(a5),a2
	lea.l	(a2,d0.w),a2
	movem.w	(a2)+,d1/d2/d3

	sub.w	d1,cth_DiffsRED(a5)
	sub.w	d2,cth_DiffsGRN(a5)
	sub.w	d3,cth_DiffsBLU(a5)
	lsr.w	#3,d0
	movem.l	(sp)+,a2/a3/a5
	movem.w	(sp)+,d2-d7
	rts

*
* Find HAM6 equivalent, or find the right RGB square and then find best pen from that square
*
_FindColorFastHAM6:
	movem.w	d2-d7,-(sp)
	movem.l	a2/a3/a5,-(sp)

	movea.l	a0,a5
	lea.l	OFlowTable(pc),a2
	move.b	(a2,d0.w),d0
	move.b	(a2,d1.w),d1
	move.b	(a2,d2.w),d2
	move.w	#$00FF,d3
	and.w	d3,d0
	and.w	d3,d1
	and.w	d3,d2
	movem.w	d0/d1/d2,cth_DiffsRED(a5)
	movem.w	cth_BaseRED(a5),d3/d4/d5
	movem.w	d3/d4/d5,cth_DiffsREDHAM(a5)

	sub.w	d0,d3
	bpl.b	*+4
	neg.w	d3
	sub.w	d1,d4
	bpl.b	*+4
	neg.w	d4
	sub.w	d2,d5
	bpl.b	*+4
	neg.w	d5

	cmp.w	d4,d5
	bls.b	.r_or_g
	cmp.w	d5,d3
	bhi.b	.modRED
	bra.b	.modBLU
.r_or_g	cmp.w	d3,d4
	bhi.b	.modGRN

.modRED	move.w	d0,d6
	lsr.w	#4,d6
	ori.w	#%100000,d6
	move.w	d4,d7
	add.w	d5,d7
	move.w	d0,cth_DiffsREDHAM(a5)
	andi.w	#$00F0,d0
	move.w	d0,cth_BaseRED(a5)
	bra.b	.exit

.modGRN	move.w	d1,d6
	lsr.w	#4,d6
	ori.w	#%110000,d6
	move.w	d3,d7
	add.w	d5,d7
	move.w	d1,cth_DiffsGRNHAM(a5)
	andi.w	#$00F0,d1
	move.w	d1,cth_BaseGRN(a5)
	bra.b	.exit

.modBLU	move.w	d2,d6
	lsr.w	#4,d6
	ori.w	#%010000,d6
	move.w	d3,d7
	add.w	d4,d7
	move.w	d2,cth_DiffsBLUHAM(a5)
	andi.w	#$00F0,d2
	move.w	d2,cth_BaseBLU(a5)

.exit	sub.w	cth_DiffsREDHAM(a5),d0
	sub.w	cth_DiffsGRNHAM(a5),d1
	sub.w	cth_DiffsBLUHAM(a5),d2
	movem.w	d0/d1/d2,cth_DiffsREDHAM(a5)

	movem.w	cth_DiffsRED(a5),d0/d1/d2
	move.w	#$00F0,d3
	and.w	d3,d0
	and.w	d3,d1
	and.w	d3,d2
	lsl.w	#6,d0
	lsl.w	#2,d1
	lsr.w	#2,d2
	or.w	d1,d0
	or.w	d2,d0
	movea.l	GlobalColorTable(pc),a2
	movea.l	(a2,d0.w),a2

	cmp.b	#2,(a2)			; when there are two entries you should
	bne	.skip

	moveq.l	#0,d0
	move.b	1(a2),d0
	lsl.w	#3,d0
	movea.l	cth_ColorTable(a5),a2
	lea.l	(a2,d0.w),a2
	movem.w	(a2),d1/d2/d3
	sub.w	d1,cth_DiffsRED(a5)
	sub.w	d2,cth_DiffsGRN(a5)
	sub.w	d3,cth_DiffsBLU(a5)
	lsr.w	#3,d0
	bra	.skip2

.skip	bsr	find_best_entry2	; select the best from the square

.skip2	move.w	cth_DiffsRED(a5),d3
	bpl.b	*+4
	neg.w	d3
	move.w	cth_DiffsBLU(a5),d4
	bpl.b	*+4
	neg.w	d4
	move.w	cth_DiffsBLU(a5),d5
	bpl.b	*+4
	neg.w	d5
	add.w	d4,d3
	add.w	d5,d3
	cmp.w	d7,d3
	bls	.normal

.ham	move.w	d6,d0
	move.w	cth_DiffsREDHAM(a5),cth_DiffsRED(a5)
	move.w	cth_DiffsGRNHAM(a5),cth_DiffsGRN(a5)
	move.w	cth_DiffsBLUHAM(a5),cth_DiffsBLU(a5)
	bra	.quit

.normal	move.w	(a2)+,cth_BaseRED(a5)
	move.w	(a2)+,cth_BaseGRN(a5)
	move.w	(a2)+,cth_BaseBLU(a5)

.quit	movem.l	(sp)+,a2/a3/a5
	movem.w	(sp)+,d2-d7
	rts

*
* Find HAM8 equivalent, or find the right RGB square and then find best pen from that square
*
_FindColorFastHAM8:
	movem.w	d2-d7,-(sp)
	movem.l	a2/a3/a5,-(sp)

	movea.l	a0,a5
	lea.l	OFlowTable(pc),a2
	move.b	(a2,d0.w),d0
	move.b	(a2,d1.w),d1
	move.b	(a2,d2.w),d2
	move.w	#$00FF,d3
	and.w	d3,d0
	and.w	d3,d1
	and.w	d3,d2

	movem.w	d0/d1/d2,cth_DiffsRED(a5)
	movem.w	cth_BaseRED(a5),d3/d4/d5
	movem.w	d3/d4/d5,cth_DiffsREDHAM(a5)

	sub.w	d0,d3
	bpl.b	*+4
	neg.w	d3
	sub.w	d1,d4
	bpl.b	*+4
	neg.w	d4
	sub.w	d2,d5
	bpl.b	*+4
	neg.w	d5

	cmp.w	d4,d5
	bls.b	.r_or_g
	cmp.w	d5,d3
	bhi.b	.modRED
	bra.b	.modBLU
.r_or_g	cmp.w	d3,d4
	bhi.b	.modGRN

.modRED	move.w	d0,d6
	lsr.w	#2,d6
	ori.w	#%10000000,d6
	move.w	d4,d7
	add.w	d5,d7
	move.w	d0,cth_DiffsREDHAM(a5)
	andi.w	#$00FC,d0
	move.w	d0,cth_BaseRED(a5)
	bra.b	.exit

.modGRN	move.w	d1,d6
	lsr.w	#2,d6
	ori.w	#%11000000,d6
	move.w	d3,d7
	add.w	d5,d7
	move.w	d1,cth_DiffsGRNHAM(a5)
	andi.w	#$00FC,d1
	move.w	d1,cth_BaseGRN(a5)
	bra.b	.exit

.modBLU	move.w	d2,d6
	lsr.w	#2,d6
	ori.w	#%01000000,d6
	move.w	d3,d7
	add.w	d4,d7
	move.w	d2,cth_DiffsBLUHAM(a5)
	andi.w	#$00FC,d2
	move.w	d2,cth_BaseBLU(a5)

.exit	sub.w	cth_DiffsREDHAM(a5),d0
	sub.w	cth_DiffsGRNHAM(a5),d1
	sub.w	cth_DiffsBLUHAM(a5),d2
	movem.w	d0/d1/d2,cth_DiffsREDHAM(a5)

	movem.w	cth_DiffsRED(a5),d0/d1/d2
	move.w	#$00F0,d3
	and.w	d3,d0
	and.w	d3,d1
	and.w	d3,d2
	lsl.w	#6,d0
	lsl.w	#2,d1
	lsr.w	#2,d2
	or.w	d1,d0
	or.w	d2,d0
	movea.l	GlobalColorTable(pc),a2
	movea.l	(a2,d0.w),a2

	cmp.b	#2,(a2)			; when there are two entries you should
	bne	.skip

	moveq.l	#0,d0
	move.b	1(a2),d0
	lsl.w	#3,d0
	movea.l	cth_ColorTable(a5),a2
	lea.l	(a2,d0.w),a2
	movem.w	(a2),d1/d2/d3
	sub.w	d1,cth_DiffsRED(a5)
	sub.w	d2,cth_DiffsGRN(a5)
	sub.w	d3,cth_DiffsBLU(a5)
	lsr.w	#3,d0
	bra	.skip2

.skip	bsr	find_best_entry2	; select the best from the square

.skip2	move.w	cth_DiffsRED(a5),d3
	bpl.b	*+4
	neg.w	d3
	move.w	cth_DiffsBLU(a5),d4
	bpl.b	*+4
	neg.w	d4
	move.w	cth_DiffsBLU(a5),d5
	bpl.b	*+4
	neg.w	d5
	add.w	d4,d3
	add.w	d5,d3
	cmp.w	d7,d3
	bls	.normal

.ham	move.w	d6,d0
	move.w	cth_DiffsREDHAM(a5),cth_DiffsRED(a5)
	move.w	cth_DiffsGRNHAM(a5),cth_DiffsGRN(a5)
	move.w	cth_DiffsBLUHAM(a5),cth_DiffsBLU(a5)
	bra	.quit

.normal	move.w	(a2)+,cth_BaseRED(a5)
	move.w	(a2)+,cth_BaseGRN(a5)
	move.w	(a2)+,cth_BaseBLU(a5)

.quit	movem.l	(sp)+,a2/a3/a5
	movem.w	(sp)+,d2-d7
	rts








find_best_entry2:			; find the best match from entries
	movem.w	d6/d7,-(sp)
	lea	PythTable(pc),a3
	lea	cth_DiffsRED(a5),a1

	movem.w	(a1),d0/d1/d2		; get original values back

	move.l	#1000000,d7		; set minimum high
	moveq.l	#0,d3			; holds the best pen so far
	moveq.l	#0,d4
	move.l	a0,d6			; store color pointer direct
	addq.l	#8,d6

.rep_c:	addq.w	#1,d4			; try next pen
	cmp.b	(a2),d4			; stop at end of square list
	beq	.exit_fb

	moveq	#0,d5
	move.b	(a2,d4.w),d5		; color in block
	lsl.w	#3,d5
	move.l	cth_ColorTable(a5),a0
	lea	(a0,d5.w),a0

	movem.w	(a1),d0/d1/d2		; get orginal color

	sub.w	(a0)+,d0		; calculate difference
	sub.w	(a0)+,d1
	sub.w	(a0)+,d2
	addq.w	#2,a0

	add.w	d0,d0
	add.w	d1,d1
	add.w	d2,d2
	move.w	(a3,d0.w),d0		; calculate Pytho's distance
	add.w	(a3,d1.w),d0
	add.w	(a3,d2.w),d0	

	cmp.l	d0,d7
	blt	.rep_c

	move.l	a0,d6			; remember color values
	move.l	d0,d7			; set new minimum
	move.b	(a2,d4.w),d3		; remember best pen nr.
	bra	.rep_c

.exit_fb:	movea.l	d6,a2			; get color pointer
	subq.w	#8,a2
	movem.w	(a2),d0/d1/d2
	sub.w	d0,cth_DiffsRED(a5)
	sub.w	d1,cth_DiffsGRN(a5)
	sub.w	d2,cth_DiffsBLU(a5)
	move.w	d3,d0			; set best color
	movem.w	(sp)+,d6/d7
	rts



find_best_entry:			; find the best match from entries
	lea	PythTable(pc),a3
	lea	cth_DiffsRED(a5),a1

	movem.w	(a1),d0/d1/d2		; get original values back

	move.l	#1000000,d7		; set minimum high
	moveq.l	#0,d3			; holds the best pen so far
	moveq.l	#0,d4
	move.l	a0,d6			; store color pointer direct
	addq.l	#8,d6

.rep_c:	addq.w	#1,d4			; try next pen
	cmp.b	(a2),d4			; stop at end of square list
	beq	.exit_fb

	moveq	#0,d5
	move.b	(a2,d4.w),d5		; color in block
	lsl.w	#3,d5
	move.l	cth_ColorTable(a5),a0
	lea	(a0,d5.w),a0

	movem.w	(a1),d0/d1/d2		; get orginal color

	sub.w	(a0)+,d0		; calculate difference
	sub.w	(a0)+,d1
	sub.w	(a0)+,d2
	addq.w	#2,a0

	add.w	d0,d0
	add.w	d1,d1
	add.w	d2,d2
	move.w	(a3,d0.w),d0		; calculate Pytho's distance
	add.w	(a3,d1.w),d0
	add.w	(a3,d2.w),d0	

	cmp.l	d0,d7
	blt	.rep_c

	move.l	a0,d6			; remember color values
	move.l	d0,d7			; set new minimum
	move.b	(a2,d4.w),d3		; remember best pen nr.
	bra	.rep_c

.exit_fb:	movea.l	d6,a1			; get color pointer
	subq.w	#8,a1
	movem.w	(a1)+,d0/d1/d2
	sub.w	d0,cth_DiffsRED(a5)
	sub.w	d1,cth_DiffsGRN(a5)
	sub.w	d2,cth_DiffsBLU(a5)
	move.w	d3,d0			; set best color
	movem.l	(sp)+,a2/a3/a5
	movem.w	(sp)+,d2-d7
	rts

****************************************************










*
* Find best pen by color RGB d0,d1,d2, ColorTableHeader a0
*
_FindColorBest:
	movem.w	d2-d7,-(sp)
	move.l	a5,-(sp)
	movea.l	a0,a5
	lea.l	OFlowTable(pc),a0
	move.b	(a0,d0.w),d0
	move.b	(a0,d1.w),d1
	move.b	(a0,d2.w),d2
	lea.l	PythTable(pc),a1
	
	moveq.l	#-1,d4
	move.w	#$ffff,cth_MinDiff(a5)
	moveq.l	#0,d5
	moveq.l	#0,d6
	moveq.l	#0,d7
	move.b	d0,d5
	move.b	d1,d6
	move.b	d2,d7
	moveq.l	#0,d0
	movea.l	cth_ColorTable(a5),a0

	move.l	a0,cth_DiffPtr(a5)
	addi.l	#8,cth_DiffPtr(a5)

.next	tst.w	(a0)
	bmi	.exit
	move.w	d5,d1
	move.w	d6,d2
	move.w	d7,d3
	sub.w	(a0)+,d1
	sub.w	(a0)+,d2
	sub.w	(a0)+,d3
	addq.w	#2,a0
	add.w	d1,d1
	add.w	d2,d2
	add.w	d3,d3
	move.w	(a1,d1.w),d1
	add.w	(a1,d2.w),d1
	add.w	(a1,d3.w),d1	
	addq.w	#1,d4
	cmp.w	cth_MinDiff(a5),d1
	bhi	.next
	move.l	a0,cth_DiffPtr(a5)
	move.w	d1,cth_MinDiff(a5)
	move.w	d4,d0
	bra	.next

.exit	movea.l	cth_DiffPtr(a5),a0
	subq.l	#8,a0
	sub.w	(a0)+,d5
	sub.w	(a0)+,d6
	sub.w	(a0)+,d7
	move.w	d5,cth_DiffsRED(a5)
	move.w	d6,cth_DiffsGRN(a5)
	move.w	d7,cth_DiffsBLU(a5)
	movea.l	(sp)+,a5
	movem.w	(sp)+,d2-d7
	rts


*
* Find best pen by color RGB d0,d1,d2, ColorTableHeader a0
* Color 0 won't be searched (special genlock mode)
*

_FindColorBestOpaque
	movem.w	d2-d7,-(sp)
	move.l	a5,-(sp)
	movea.l	a0,a5
	lea.l	OFlowTable(pc),a0
	move.b	(a0,d0.w),d0
	move.b	(a0,d1.w),d1
	move.b	(a0,d2.w),d2
	andi.w	#255,d0
	andi.w	#255,d1
	andi.w	#255,d2
	lea.l	PythTable(pc),a1
	
	moveq.l	#0,d4
	move.w	#$ffff,cth_MinDiff(a5)
	move.w	d0,d5
	move.w	d1,d6
	move.w	d2,d7
	moveq.l	#1,d0
	movea.l	cth_ColorTable(a5),a0
	addq.w	#8,a0

	move.l	a0,cth_DiffPtr(a5)
	addi.l	#6,cth_DiffPtr(a5)

.next	tst.w	(a0)
	bmi	.exit
	move.w	d5,d1
	move.w	d6,d2
	move.w	d7,d3
	sub.w	(a0)+,d1
	sub.w	(a0)+,d2
	sub.w	(a0)+,d3
	addq.w	#2,a0
	add.w	d1,d1
	add.w	d2,d2
	add.w	d3,d3
	move.w	(a1,d1.w),d1
	add.w	(a1,d2.w),d1
	add.w	(a1,d3.w),d1	
	addq.w	#1,d4
	cmp.w	cth_MinDiff(a5),d1
	bhi	.next
	move.l	a0,cth_DiffPtr(a5)
	move.w	d1,cth_MinDiff(a5)
	move.w	d4,d0
	bra	.next

.exit	movea.l	cth_DiffPtr(a5),a0
	subq.l	#8,a0
	sub.w	(a0)+,d5
	sub.w	(a0)+,d6
	sub.w	(a0)+,d7
	move.w	d5,cth_DiffsRED(a5)
	move.w	d6,cth_DiffsGRN(a5)
	move.w	d7,cth_DiffsBLU(a5)
	movea.l	(sp)+,a5
	movem.w	(sp)+,d2-d7
	rts


	dcb.b	$ff,$00
OFlowTable
	dc.b	$00,$01,$02,$03,$04,$05,$06,$07
	dc.b	$08,$09,$0a,$0b,$0c,$0d,$0e,$0f
	dc.b	$10,$11,$12,$13,$14,$15,$16,$17
	dc.b	$18,$19,$1a,$1b,$1c,$1d,$1e,$1f
	dc.b	$20,$21,$22,$23,$24,$25,$26,$27
	dc.b	$28,$29,$2a,$2b,$2c,$2d,$2e,$2f
	dc.b	$30,$31,$32,$33,$34,$35,$36,$37
	dc.b	$38,$39,$3a,$3b,$3c,$3d,$3e,$3f
	dc.b	$40,$41,$42,$43,$44,$45,$46,$47
	dc.b	$48,$49,$4a,$4b,$4c,$4d,$4e,$4f
	dc.b	$50,$51,$52,$53,$54,$55,$56,$57
	dc.b	$58,$59,$5a,$5b,$5c,$5d,$5e,$5f
	dc.b	$60,$61,$62,$63,$64,$65,$66,$67
	dc.b	$68,$69,$6a,$6b,$6c,$6d,$6e,$6f
	dc.b	$70,$71,$72,$73,$74,$75,$76,$77
	dc.b	$78,$79,$7a,$7b,$7c,$7d,$7e,$7f
	dc.b	$80,$81,$82,$83,$84,$85,$86,$87
	dc.b	$88,$89,$8a,$8b,$8c,$8d,$8e,$8f
	dc.b	$90,$91,$92,$93,$94,$95,$96,$97
	dc.b	$98,$99,$9a,$9b,$9c,$9d,$9e,$9f
	dc.b	$a0,$a1,$a2,$a3,$a4,$a5,$a6,$a7
	dc.b	$a8,$a9,$aa,$ab,$ac,$ad,$ae,$af
	dc.b	$b0,$b1,$b2,$b3,$b4,$b5,$b6,$b7
	dc.b	$b8,$b9,$ba,$bb,$bc,$bd,$be,$bf
	dc.b	$c0,$c1,$c2,$c3,$c4,$c5,$c6,$c7
	dc.b	$c8,$c9,$ca,$cb,$cc,$cd,$ce,$cf
	dc.b	$d0,$d1,$d2,$d3,$d4,$d5,$d6,$d7
	dc.b	$d8,$d9,$da,$db,$dc,$dd,$de,$df
	dc.b	$e0,$e1,$e2,$e3,$e4,$e5,$e6,$e7
	dc.b	$e8,$e9,$ea,$eb,$ec,$ed,$ee,$ef
	dc.b	$f0,$f1,$f2,$f3,$f4,$f5,$f6,$f7
	dc.b	$f8,$f9,$fa,$fb,$fc,$fd,$fe,$ff
	dcb.b	$ff,$ff

	dc.w	$0000,$3f80,$3f01,$3e82,$3e04,$3d86,$3d09,$3c8c
	dc.w	$3c10,$3b94,$3b19,$3a9e,$3a24,$39aa,$3931,$38b8
	dc.w	$3840,$37c8,$3751,$36da,$3664,$35ee,$3579,$3504
	dc.w	$3490,$341c,$33a9,$3336,$32c4,$3252,$31e1,$3170
	dc.w	$3100,$3090,$3021,$2fb2,$2f44,$2ed6,$2e69,$2dfc
	dc.w	$2d90,$2d24,$2cb9,$2c4e,$2be4,$2b7a,$2b11,$2aa8
	dc.w	$2a40,$29d8,$2971,$290a,$28a4,$283e,$27d9,$2774
	dc.w	$2710,$26ac,$2649,$25e6,$2584,$2522,$24c1,$2460
	dc.w	$2400,$23a0,$2341,$22e2,$2284,$2226,$21c9,$216c
	dc.w	$2110,$20b4,$2059,$1ffe,$1fa4,$1f4a,$1ef1,$1e98
	dc.w	$1e40,$1de8,$1d91,$1d3a,$1ce4,$1c8e,$1c39,$1be4
	dc.w	$1b90,$1b3c,$1ae9,$1a96,$1a44,$19f2,$19a1,$1950
	dc.w	$1900,$18b0,$1861,$1812,$17c4,$1776,$1729,$16dc
	dc.w	$1690,$1644,$15f9,$15ae,$1564,$151a,$14d1,$1488
	dc.w	$1440,$13f8,$13b1,$136a,$1324,$12de,$1299,$1254
	dc.w	$1210,$11cc,$1189,$1146,$1104,$10c2,$1081,$1040
	dc.w	$1000,$0fc0,$0f81,$0f42,$0f04,$0ec6,$0e89,$0e4c
	dc.w	$0e10,$0dd4,$0d99,$0d5e,$0d24,$0cea,$0cb1,$0c78
	dc.w	$0c40,$0c08,$0bd1,$0b9a,$0b64,$0b2e,$0af9,$0ac4
	dc.w	$0a90,$0a5c,$0a29,$09f6,$09c4,$0992,$0961,$0930
	dc.w	$0900,$08d0,$08a1,$0872,$0844,$0816,$07e9,$07bc
	dc.w	$0790,$0764,$0739,$070e,$06e4,$06ba,$0691,$0668
	dc.w	$0640,$0618,$05f1,$05ca,$05a4,$057e,$0559,$0534
	dc.w	$0510,$04ec,$04c9,$04a6,$0484,$0462,$0441,$0420
	dc.w	$0400,$03e0,$03c1,$03a2,$0384,$0366,$0349,$032c
	dc.w	$0310,$02f4,$02d9,$02be,$02a4,$028a,$0271,$0258
	dc.w	$0240,$0228,$0211,$01fa,$01e4,$01ce,$01b9,$01a4
	dc.w	$0190,$017c,$0169,$0156,$0144,$0132,$0121,$0110
	dc.w	$0100,$00f0,$00e1,$00d2,$00c4,$00b6,$00a9,$009c
	dc.w	$0090,$0084,$0079,$006e,$0064,$005a,$0051,$0048
	dc.w	$0040,$0038,$0031,$002a,$0024,$001e,$0019,$0014
	dc.w	$0010,$000c,$0009,$0006,$0004,$0002,$0001,$0000
PythTable:
	dc.w	$0000,$0000,$0001,$0002,$0004,$0006,$0009,$000c
	dc.w	$0010,$0014,$0019,$001e,$0024,$002a,$0031,$0038
	dc.w	$0040,$0048,$0051,$005a,$0064,$006e,$0079,$0084
	dc.w	$0090,$009c,$00a9,$00b6,$00c4,$00d2,$00e1,$00f0
	dc.w	$0100,$0110,$0121,$0132,$0144,$0156,$0169,$017c
	dc.w	$0190,$01a4,$01b9,$01ce,$01e4,$01fa,$0211,$0228
	dc.w	$0240,$0258,$0271,$028a,$02a4,$02be,$02d9,$02f4
	dc.w	$0310,$032c,$0349,$0366,$0384,$03a2,$03c1,$03e0
	dc.w	$0400,$0420,$0441,$0462,$0484,$04a6,$04c9,$04ec
	dc.w	$0510,$0534,$0559,$057e,$05a4,$05ca,$05f1,$0618
	dc.w	$0640,$0668,$0691,$06ba,$06e4,$070e,$0739,$0764
	dc.w	$0790,$07bc,$07e9,$0816,$0844,$0872,$08a1,$08d0
	dc.w	$0900,$0930,$0961,$0992,$09c4,$09f6,$0a29,$0a5c
	dc.w	$0a90,$0ac4,$0af9,$0b2e,$0b64,$0b9a,$0bd1,$0c08
	dc.w	$0c40,$0c78,$0cb1,$0cea,$0d24,$0d5e,$0d99,$0dd4
	dc.w	$0e10,$0e4c,$0e89,$0ec6,$0f04,$0f42,$0f81,$0fc0
	dc.w	$1000,$1040,$1081,$10c2,$1104,$1146,$1189,$11cc
	dc.w	$1210,$1254,$1299,$12de,$1324,$136a,$13b1,$13f8
	dc.w	$1440,$1488,$14d1,$151a,$1564,$15ae,$15f9,$1644
	dc.w	$1690,$16dc,$1729,$1776,$17c4,$1812,$1861,$18b0
	dc.w	$1900,$1950,$19a1,$19f2,$1a44,$1a96,$1ae9,$1b3c
	dc.w	$1b90,$1be4,$1c39,$1c8e,$1ce4,$1d3a,$1d91,$1de8
	dc.w	$1e40,$1e98,$1ef1,$1f4a,$1fa4,$1ffe,$2059,$20b4
	dc.w	$2110,$216c,$21c9,$2226,$2284,$22e2,$2341,$23a0
	dc.w	$2400,$2460,$24c1,$2522,$2584,$25e6,$2649,$26ac
	dc.w	$2710,$2774,$27d9,$283e,$28a4,$290a,$2971,$29d8
	dc.w	$2a40,$2aa8,$2b11,$2b7a,$2be4,$2c4e,$2cb9,$2d24
	dc.w	$2d90,$2dfc,$2e69,$2ed6,$2f44,$2fb2,$3021,$3090
	dc.w	$3100,$3170,$31e1,$3252,$32c4,$3336,$33a9,$341c
	dc.w	$3490,$3504,$3579,$35ee,$3664,$36da,$3751,$37c8
	dc.w	$3840,$38b8,$3931,$39aa,$3a24,$3a9e,$3b19,$3b94
	dc.w	$3c10,$3c8c,$3d09,$3d86,$3e04,$3e82,$3f01,$3f80


GlobalColorTable		dc.l	0
GlobalTableValid		dc.l	FALSE
GlobalTableOpaque		dc.w	0

;-- END OF FILE ----------------------------------------------------------
